function(get_package_version instr pname out_var)
    set(ver_regex "^${pname}[-_]([0-9]+)[._]([0-9]+)[._]([0-9a-z]+)$")
    string(REGEX REPLACE ${ver_regex} "\\1" vmaj ${instr})
    string(REGEX REPLACE ${ver_regex} "\\2" vmin ${instr})
    string(REGEX REPLACE ${ver_regex} "\\3" vpatch ${instr})
    set(${out_var}_VERSION_MAJOR ${vmaj} PARENT_SCOPE)
    set(${out_var}_VERSION_MINOR ${vmin} PARENT_SCOPE)
    set(${out_var}_VERSION_PATCH ${vpatch} PARENT_SCOPE)
    set(${out_var}_VERSION "${vmaj}.${vmin}.${vpatch}" PARENT_SCOPE)
endfunction()

function(get_package_version_2 instr pname out_var)
    set(ver_regex "^${pname}[-_]([0-9]+)[._]([0-9]+)$")
    string(REGEX REPLACE ${ver_regex} "\\1" vmaj ${instr})
    string(REGEX REPLACE ${ver_regex} "\\2" vmin ${instr})
    set(${out_var}_VERSION_MAJOR ${vmaj} PARENT_SCOPE)
    set(${out_var}_VERSION_MINOR ${vmin} PARENT_SCOPE)
    set(${out_var}_VERSION "${vmaj}.${vmin}" PARENT_SCOPE)
endfunction()

function(build_status component version status)
    string(LENGTH ${component} comp_len)
    string(LENGTH ${version} ver_len)
    math(EXPR n_dots "24 - ${comp_len} - ${ver_len} - 4")
    string(SUBSTRING "........................" 0 ${n_dots} dots)
    message(STATUS "${component} (${version}) ${dots} ${status}")
endfunction()
